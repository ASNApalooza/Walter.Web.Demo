Using System
Using System.Data
Using System.Configuration
Using System.Collections
Using System.Web
Using System.Web.Security
Using System.Web.UI
Using System.Web.UI.WebControls
Using System.Web.UI.WebControls.WebParts
Using System.Web.UI.HtmlControls
Using ASNA.DataGate.Client
Using ASNA.DataGate.Common
Using ASNA.DataGate.DataLink
Using ASNA.DataGate.Providers
Using ASNA.Helpers.DataServices
Using ASNA.VisualRPG.Runtime
Using System.Text.RegularExpressions

BegClass views_DataServicesGridView Partial(*Yes) Access(*Public) Extends(System.Web.UI.Page)
    DclFld DEFAULT_SORT_ORDER Type(*Integer4) Inz(OrderBy.Number)
    DclFld DBNAME             Type(*String) 

    DclFld CurrentOrderBy Type(*Integer4)
    DclFld CMNameValue    Type(*String)
    DclFld CMCustNoValue  Type(*Integer4) 

    BegEnum OrderBy 
        DclEnumFld Name
        DclENumFld Number
    EndEnum

    BegSr Page_Load Access(*Private) Event(*This.Load)
        DclSrParm sender Type(*Object)
        DclSrParm e Type(System.EventArgs)

        Page.Title = "DataList"
        (*This.Master *As Home).PageHeading = "Walter: The AVR file object that's got your back!"
        DBNAME = (*This.Master *As Home).DBName

        If (NOT Page.IsPostBack)
            CurrentOrderBy = OrderBy.Name
            Viewstate["orderby"] = CurrentOrderBy 
            SetPositionToValueKeyValues(String.Empty, 0)
            LoadGrid()
        Else 
            CurrentOrderBy = Viewstate["orderby"].ToString()
        EndIf
    EndSr

    BegSr SetPositionToValueKeyValues 
        DclSrParm Name   Type(*String)
        DclSrParm Number Type(*Integer4) 

        CMNameValue   = Name
        CMCustNoValue = Number 
    EndSr

    BegSr gridviewCustomers_PreRender Access(*Private) Event(*This.gridviewCustomers.PreRender)
        DclSrParm sender Type(*Object)
        DclSrParm e Type(System.EventArgs)

        ASNA.Helpers.ASPNET.Controls.AddHTMLHeaderAndFooter(gridviewCustomers)
    EndSr
    
	BegSr linkbuttonNext_Click Access(*Private) Event(*This.linkbuttonNext.Click)
		DclSrParm sender Type(*Object)
		DclSrParm e Type(System.EventArgs)

        DclFld QueryState Type(*String) 

        QueryState = Viewstate["queryState"].ToString() 
        LoadGrid(QueryState) 
    EndSr

    BegSr ConfigureDataList
        DclSrParm Query     Type(ASNA.Helpers.DataServices.QueryDriver)

        Query.Args.Inputs.Library      = "examples"
        Query.Args.Inputs.File         = "CmastNewL2"
        Query.Args.Inputs.FieldsList   = "CMCUSTNO,CMNAME"
        Query.Args.Inputs.Rows         = 12

        If (CurrentOrderBy = OrderBy.Name) 
            Query.Args.Inputs.File      = "CmastNewL2"
            Query.Args.Inputs.QueryParms.Add(*New ASNA.Helpers.DataServices.QueryField("CMNAME",*This.CMNameValue))
            Query.Args.Inputs.QueryParms.Add(*New ASNA.Helpers.DataServices.QueryField("CMCUSTNO",*This.CMCustNoValue))
        ElseIf (CurrentOrderBy = OrderBy.Number)
            Query.Args.Inputs.File      = "CmastNewL1"        
            Query.Args.Inputs.QueryParms.Add(*New ASNA.Helpers.DataServices.QueryField("CMCUSTNO",*This.CMCustNoValue))        
        EndIf             
    EndSr 

    BegSr LoadGrid
        DclFld DGDB     Type(AdgConnection) 
        DclFld Query    Type(ASNA.Helpers.DataServices.QueryDriver) 

        LoadGrid(*Nothing) 
    EndSr 
    
    BegSr LoadGrid
        DclSrParm QueryState Type(*String) 

        DclFld DGDB       Type(AdgConnection) 
        DclFld Query      Type(ASNA.Helpers.DataServices.QueryDriver) 

        DGDB = *New AdgConnection(*This.DBName)
        Query = *New ASNA.Helpers.DataServices.QueryDriver(DGDB) 
        If (String.IsNullOrEmpty(QueryState)) 
            ConfigureDataList(Query)
            gridviewCustomers.DataSource = Query.Exec() *As DataTable 
        Else 
            gridviewCustomers.DataSource = Query.NextPage(QueryState) *As DataTable                      
        EndIf 

        If (Query.Args.Error.ErrorException = *Nothing)
            Viewstate["queryState"] = Query.Args.Outputs.QueryState
            gridviewCustomers.DataBind()
        EndIf

        DGDB.Close()
    EndSr
	
	BegSr linkbuttonPosition_Click Access(*Private) Event(*This.linkbuttonPosition.Click)
		DclSrParm sender Type(*Object)
		DclSrParm e Type(System.EventArgs)

        If (CurrentOrderBy = OrderBy.Name) 
            *This.CMNameValue = textboxPosition.Text.Trim()
            *This.CMCustNoValue = 0
        ElseIf (CurrentOrderBy = OrderBy.Number)
            *This.CMCustNoValue = textboxPosition.Text.Trim()
        EndIf             

        LoadGrid()
        textboxPosition.Text = String.Empty 
	EndSr
    
	BegSr linkbuttonFirst_Click Access(*Private) Event(*This.linkbuttonFirst.Click)
		DclSrParm sender Type(*Object)
		DclSrParm e Type(System.EventArgs)

        SetPositionToValueKeyValues(String.Empty, 0)
        LoadGrid()
	EndSr

	BegSr linkbuttonByName_Click Access(*Private) Event(*This.linkbuttonByName.Click)
		DclSrParm sender Type(*Object)
		DclSrParm e Type(System.EventArgs)
	
        CurrentOrderBy = OrderBy.Name
        labelSortBy.Text = "By name"
        Viewstate["orderby"] = CurrentOrderBy 
        SetPositionToValueKeyValues(String.Empty, 0)
        LoadGrid()
	EndSr

	BegSr linkbuttonByNumber_Click Access(*Private) Event(*This.linkbuttonByNumber.Click)
		DclSrParm sender Type(*Object)
		DclSrParm e Type(System.EventArgs)
		
        CurrentOrderBy = OrderBy.Number
        labelSortBy.Text = "By number"
        Viewstate["orderby"] = CurrentOrderBy 
        SetPositionToValueKeyValues(String.Empty, 0)
        LoadGrid()
	EndSr
EndClass
