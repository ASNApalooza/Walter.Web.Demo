Using System
Using System.Data
Using System.Configuration
Using System.Collections
Using System.Web
Using System.Web.Security
Using System.Web.UI
Using System.Web.UI.WebControls
Using System.Web.UI.WebControls.WebParts
Using System.Web.UI.HtmlControls

BegClass views_TraditionalGridView Partial(*Yes) Access(*Public) Extends(System.Web.UI.Page)

    DclFld DBNAME Type(*String) 
    DclDB pgmDB DBName( "*PUBLIC/DG Net Local" )

    DclDiskFile  CustomerByName +
          Type( *Input  ) +
          Org( *Indexed ) +
          Prefix( Customer_ ) +
          File( "Examples/CMastNewL2" ) +
          DB( pgmDB ) +
          ImpOpen( *No )
              
    DclMemoryFile CustomerByNameMF ImpOpen( *Yes ) 
        DclRecordFormat Customers 
        DclRecordFld    Customer_CMCustNo  Type( *Packed ) Len( 9,0 )
        DclRecordFld    Customer_CMName    Type( *Char ) Len( 40 )     
        
    BegSr Page_Load Access(*Private) Event(*This.Load)
        DclSrParm sender Type(*Object)
        DclSrParm e Type(System.EventArgs)

        DBNAME = (*This.Master *As Home).DBName

        pgmDB.DBName = DBNAME
        Connect pgmDB 
        Open CustomerByName

        If (NOT Page.IsPostBack)
            LoadGrid()
        Else
        EndIf
    EndSr

    BegSr Page_Unload Access(*Private) Event(*This.Unload)
        DclSrParm sender Type(*Object)
        DclSrParm e Type(System.EventArgs)

        Close *ALL
        Disconnect pgmDB
    EndSr

    BegSr LoadGrid
        Do FromVal( 1 ) ToVal( 12 )
            Read CustomerByName 
            If ( CustomerByName.IsEof ) 
                Leave
            EndIf

            Write CustomerByNameMF 
        EndDo

        Viewstate["custno"] = Customer_CMCustNo
        Viewstate["name"] = Customer_CMName 

        gridviewCustomers.DataSource = CustomerByNameMF.DataSet
        gridViewCustomers.DataBind()  
    EndSr 

	BegSr linkbuttonNext_Click Access(*Private) Event(*This.linkbuttonNext.Click)
		DclSrParm sender Type(*Object)
		DclSrParm e Type(System.EventArgs)

        Customer_CMName = Viewstate["name"].ToString()
        Customer_CMCustNo = Viewstate["custno"].ToString()

        SetGT CustomerByName Key(Customer_CMName, Customer_CMCustNo) 
        LoadGrid()		
	EndSr

	BegSr linkbuttonPosition_Click Access(*Private) Event(*This.linkbuttonPosition.Click)
		DclSrParm sender Type(*Object)
		DclSrParm e Type(System.EventArgs)

        Customer_CMName = textboxPosition.Text.Trim()
        Customer_CMCustNo = 0

        SetLL CustomerByName Key(Customer_CMName, Customer_CMCustNo) 
        LoadGrid()				
	EndSr

EndClass
